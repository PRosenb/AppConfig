import org.ajoberstar.grgit.Grgit

import java.text.SimpleDateFormat

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.gitlab.arturbosch.detekt'

ext {
    // https://github.com/krzema12/fsynth/issues/53#issuecomment-622530487
    def repo = Grgit.open(Collections.singletonMap("currentDir", project.rootDir))
    def head = repo.head()
    def tags = repo.tag.list().findAll { it.commit == head }
    if (tags.size() > 1) {
        def errorStr = "More than one tag: "
        tags.each { item ->
            errorStr += item.name + ", "
        }
        errorStr = errorStr.substring(0, errorStr.length() - 2)
        throw new IllegalArgumentException(errorStr)
    } else if (tags.size() == 1) {
        def currentTag = tags[0].name
        assert currentTag ==~ '^v[0-9]+[.][0-9]+.[0-9]+$'

        calculatedVersion = currentTag.substring(1) // remove leading "v"
    } else {
        SimpleDateFormat formatter = new SimpleDateFormat("yyyyMMddHHmm")
        calculatedVersion = formatter.format(new Date())
    }

    println("version: " + calculatedVersion)
}

version calculatedVersion
apply from: 'maven-publish.gradle'

android {
    compileSdkVersion 33

    defaultConfig {
        minSdkVersion 16
        setProperty("archivesBaseName", "AppConfig_" + version)
    }

    lintOptions {
        // see also kotlinOptions.allWarningsAsErrors below
        warningsAsErrors true
        abortOnError true
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

dependencies {
    implementation "androidx.preference:preference-ktx:1.2.0"

    testImplementation 'org.mockito:mockito-inline:3.2.4'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.6.28'
    testImplementation "com.nhaarman:mockito-kotlin:1.6.0"
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.7.1'
    testImplementation group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: '1.7.0'
    testImplementation "org.assertj:assertj-core:3.18.1"
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions.allWarningsAsErrors = true
}

detekt {
    // fail build on any finding
    failFast = true
    // preconfigure defaults
    buildUponDefaultConfig = true
    config = files("$projectDir/../config/detekt.yml")

    reports {
        // observe findings in your browser with structure and code snippets
        html.enabled = true
        // checkstyle like format mainly for integrations like Jenkins
        xml.enabled = false
        // similar to the console output, contains issue signature to manually edit baseline files
        txt.enabled = false
    }
}
